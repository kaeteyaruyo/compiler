/* Definition section */
%{
#include "y.tab.h"	/* header file generated by bison */
#include "string.h"

extern YYSTYPE yylval; //YYSTYPE
char buf[256];
char strbuf[256];
int dumpFlagWithScope = 0;

extern int currentScope;
extern int errorFlag;
extern char errorMsg[256];
extern void dump_symbol(int scope);
extern void yyerror(char *s);

/* This macro is used to record code line */
#define CONCAT { strncat(buf, yytext, 256 - strlen(buf) - 1); }
%}

/* Define regular expression label */
digit		[0-9]
letter		[a-zA-Z]
integer     {digit}+
float       {digit}+"."{digit}+
id			{letter}+({letter}|{digit})*

%option yylineno

%x COMMENT STRING_STATE

/* Rules section */
%%

 /* Arithmetic */
"+"     { CONCAT; return '+'; }
"-" 	{ CONCAT; return '-'; }
"*" 	{ CONCAT; return '*'; }
"/" 	{ CONCAT; return '/'; }
"%"     { CONCAT; return '%'; }
"++"    { CONCAT; return INC_OP; }
"--"    { CONCAT; return DEC_OP; }

 /* Relational */
">"		{ CONCAT; return '>'; }
"<"		{ CONCAT; return '<'; }
">="	{ CONCAT; return GE_OP; }
"<="	{ CONCAT; return LE_OP; }
"=="	{ CONCAT; return EQ_OP; }
"!="	{ CONCAT; return NE_OP; }

 /* Assignment */
"="		{ CONCAT; return '='; }
"+="	{ CONCAT; return ADD_ASSIGN; }
"-="	{ CONCAT; return SUB_ASSIGN; }
"*="	{ CONCAT; return MUL_ASSIGN; }
"/="	{ CONCAT; return DIV_ASSIGN; }
"%="	{ CONCAT; return MOD_ASSIGN; }

 /* Delimiters */
"("		{ CONCAT; return '('; }
")"		{ CONCAT; return ')'; }
"{"		{ CONCAT; ++currentScope; return '{'; }
"}"		{ CONCAT; dumpFlagWithScope = currentScope; --currentScope; return '}'; }
"["		{ CONCAT; return '['; }
"]"		{ CONCAT; return ']'; }
","		{ CONCAT; return ','; }

 /* Print Keywords */
"print"		{ CONCAT; return PRINT; }

 /* Condition and Loop Keywords */
"if"		{ CONCAT; return IF; }
"else"		{ CONCAT; return ELSE; }
"while"		{ CONCAT; return WHILE; }

"return"    { CONCAT; return RETURN; }

 /* Declaration Keywords */
"void"		{
    CONCAT;
    yylval.string = strdup(yytext);
    return VOID;
}
"int"		{
    CONCAT;
    yylval.string = strdup(yytext);
    return INT;
}
"float"  	{
    CONCAT;
    yylval.string = strdup(yytext);
    return FLOAT;
}
"string"  	{
    CONCAT;
    yylval.string = strdup(yytext);
    return STRING;
}
"bool"  	{
    CONCAT;
    yylval.string = strdup(yytext);
    return BOOL;
}

 /* boolean Keywords */
"true"      {
    CONCAT;
    yylval.token.type = strdup("bool");
    yylval.token.value = strdup("1");
    return TRUE;
}
"false"     {
    CONCAT;
    yylval.token.type = strdup("bool");
    yylval.token.value = strdup("0");
    return FALSE;
}

 /* Variable ID */
 {id}       {
    CONCAT;
    yylval.string = strdup(yytext);
    return ID;
}

 /* Number Constant */
{integer}	{
    CONCAT;
    yylval.token.type = strdup("int");
    yylval.token.value = strdup(yytext);
    return I_CONST;
}
{float}	    {
    CONCAT;
    yylval.token.type = strdup("float");
    yylval.token.value = strdup(yytext);
    return F_CONST;
}

 /* String Constant */
\"				        {
    BEGIN STRING_STATE;
    CONCAT;
    memset(strbuf, 0, sizeof(strbuf));
    strcpy(strbuf, yytext);
}
<STRING_STATE>\"		{
    BEGIN INITIAL;
    CONCAT;
    strcat(strbuf, yytext);
    yylval.token.type = strdup("string");
    yylval.token.value = strdup(strbuf);
    return STRING_LITERAL;
}
<STRING_STATE>[^"]*		{
    CONCAT;
    strcat(strbuf, yytext);
}

 /* C type Comment */
"/*"				{ BEGIN COMMENT; CONCAT; }
<COMMENT>[^*\n]+	{ CONCAT; }
<COMMENT>\n			{
    if(strlen(buf) != 0)
        printf("%d: %s\n", yylineno, buf);
    else{
        printf("%d:\n", yylineno);
    }
    memset(buf, 0, sizeof(buf));
}
<COMMENT>"*"		{ CONCAT; }
<COMMENT>"*/"		{ BEGIN INITIAL; CONCAT; }

 /* C++ type Comment */
\/\/.*	{ CONCAT; }

 /* others */
[;]         { CONCAT; return ';'; }
[\n]        {
    if(strlen(buf) != 0){
        printf("%d: %s\n", yylineno, buf);
        if(errorFlag > 0){
            yyerror(errorMsg);
            errorFlag = 0;
        }
        if(dumpFlagWithScope > 0){
            dump_symbol(dumpFlagWithScope);
            dumpFlagWithScope = 0;
        }
    }
    else{
        printf("%d:\n", yylineno);
    }
    memset(buf, 0, sizeof(buf));
}
[ \t]		{ CONCAT; } /* Ignore */
[^ \t\n] 	{ CONCAT; } /* Ignore other charactor sets */
<<eof>>     { dump_symbol(0); printf("\nTotal lines: %d \n", yylineno); return 0; }

%%

int yywrap()
{
    return 1;
}
