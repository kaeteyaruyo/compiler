/* Definition section */
%{
#include "y.tab.h"	/* header file generated by bison */
#include "string.h"

extern YYSTYPE yylval; //YYSTYPE
char buf[256];
char strbuf[256];
int dumpFlagWithScope = 0;

extern int currentScope;
extern int semanticErrorFlag;
extern char errorMsg[256];
extern void dump_symbol(int scope);
extern void yyerror(char *s);

/* This macro is used to record code line */
#define CONCAT { strncat(buf, yytext, 256 - strlen(buf) - 1); }
%}

/* Define regular expression label */
digit		[0-9]
letter		[a-zA-Z]
integer     {digit}+
float       {digit}+"."{digit}+
id			{letter}+({letter}|{digit})*

%option yylineno

%x COMMENT STRING_STATE

/* Rules section */
%%

 /* Arithmetic */
"+"     { CONCAT; return '+'; }
"-" 	{ CONCAT; return '-'; }
"*" 	{ CONCAT; return '*'; }
"/" 	{ CONCAT; return '/'; }
"%"     { CONCAT; return '%'; }
"++"    { CONCAT; return INC_OP; }
"--"    { CONCAT; return DEC_OP; }

 /* Relational */
">"		{ CONCAT; return '>'; }
"<"		{ CONCAT; return '<'; }
">="	{ CONCAT; return GE_OP; }
"<="	{ CONCAT; return LE_OP; }
"=="	{ CONCAT; return EQ_OP; }
"!="	{ CONCAT; return NE_OP; }

 /* Assignment */
"="		{ CONCAT; return '='; }
"+="	{ CONCAT; return ADD_ASSIGN; }
"-="	{ CONCAT; return SUB_ASSIGN; }
"*="	{ CONCAT; return MUL_ASSIGN; }
"/="	{ CONCAT; return DIV_ASSIGN; }
"%="	{ CONCAT; return MOD_ASSIGN; }

 /* Delimiters */
"("		{ CONCAT; return '('; }
")"		{ CONCAT; return ')'; }
"{"		{ CONCAT; ++currentScope; return '{'; }
"}"		{ CONCAT; dumpFlagWithScope = currentScope; --currentScope; return '}'; }
"["		{ CONCAT; return '['; }
"]"		{ CONCAT; return ']'; }
","		{ CONCAT; return ','; }

 /* Print Keywords */
"print"		{ CONCAT; return PRINT; }

 /* Condition and Loop Keywords */
"if"		{ CONCAT; return IF; }
"else"		{ CONCAT; return ELSE; }
"while"		{ CONCAT; return WHILE; }

"return"    { CONCAT; return RETURN; }

 /* Declaration Keywords */
"void"		{
    CONCAT;
    sprintf(yylval.string, "%s", yytext);
    return VOID;
}
"int"		{
    CONCAT;
    sprintf(yylval.string, "%s", yytext);
    return INT;
}
"float"  	{
    CONCAT;
    sprintf(yylval.string, "%s", yytext);
    return FLOAT;
}
"string"  	{
    CONCAT;
    sprintf(yylval.string, "%s", yytext);
    return STRING;
}
"bool"  	{
    CONCAT;
    sprintf(yylval.string, "%s", yytext);
    return BOOL;
}

 /* boolean Keywords */
"true"      {
    CONCAT;
    sprintf(yylval.token.type, "bool");
    sprintf(yylval.token.value, "1");
    return TRUE;
}
"false"     {
    CONCAT;
    sprintf(yylval.token.type, "bool");
    sprintf(yylval.token.value, "0");
    return FALSE;
}

 /* Variable ID */
 {id}       {
    CONCAT;
    sprintf(yylval.string, "%s", yytext);
    return ID;
}

 /* Number Constant */
{integer}	{
    CONCAT;
    sprintf(yylval.token.type, "int");
    sprintf(yylval.token.value, "%s", yytext);
    return I_CONST;
}
{float}	    {
    CONCAT;
    sprintf(yylval.token.type, "float");
    sprintf(yylval.token.value, "%s", yytext);
    return F_CONST;
}

 /* String Constant */
\"				        {
    BEGIN STRING_STATE;
    CONCAT;
    memset(strbuf, 0, sizeof(strbuf));
    strcpy(strbuf, yytext);
}
<STRING_STATE>\"		{
    BEGIN INITIAL;
    CONCAT;
    strcat(strbuf, yytext);
    sprintf(yylval.token.type, "string");
    sprintf(yylval.token.value, "%s", strbuf);
    return STRING_LITERAL;
}
<STRING_STATE>[^"]*		{
    CONCAT;
    strcat(strbuf, yytext);
}

 /* C type Comment */
"/*"				{ BEGIN COMMENT; CONCAT; }
<COMMENT>[^*\n]+	{ CONCAT; }
<COMMENT>\n			{
    if(strlen(buf) != 0)
        printf("%d: %s\n", yylineno, buf);
    else{
        printf("%d:\n", yylineno);
    }
    memset(buf, 0, sizeof(buf));
}
<COMMENT>"*"		{ CONCAT; }
<COMMENT>"*/"		{ BEGIN INITIAL; CONCAT; }

 /* C++ type Comment */
\/\/.*	{ CONCAT; }

 /* others */
[;]         { CONCAT; return ';'; }
[\n]        {
    if(strlen(buf) != 0){
        printf("%d: %s\n", yylineno, buf);
        if(semanticErrorFlag > 0){
            yyerror(errorMsg);
            semanticErrorFlag = 0;
        }
        if(dumpFlagWithScope > 0){
            dump_symbol(dumpFlagWithScope);
            dumpFlagWithScope = 0;
        }
    }
    else{
        printf("%d:\n", yylineno);
    }
    memset(buf, 0, sizeof(buf));
}
[ \t]		{ CONCAT; } /* Ignore */
[^ \t\n] 	{ CONCAT; } /* Ignore other charactor sets */
<<eof>>     { dump_symbol(0); printf("\nTotal lines: %d \n", yylineno); return 0; }

%%

int yywrap()
{
    return 1;
}
